buildscript {
    ext.kotlin_version = '1.9.24'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def reactNative = rootProject.allprojects
    .find { it.name == 'ReactAndroid' }

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 34)
    
    namespace "com.jsonevalrs"
    
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 34)
        
        // Android 15+ 16KB page size supported via linker flags in CMakeLists.txt
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -fexceptions -frtti"
                arguments "-DANDROID_STL=c++_shared"
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
   
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    buildFeatures {
        prefab true
    }
    
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        // Exclude React Native libraries - they're provided by the app
        // This prevents duplicate .so files and eliminates the need for pickFirst in the app
        exclude 'lib/**/libjsi.so'
        exclude 'lib/**/libc++_shared.so'
        exclude 'lib/**/libreactnativejni.so'
        exclude 'lib/**/libfbjni.so'
        
        // Keep only our module's native library
        // pickFirst is used as fallback for any remaining conflicts
        pickFirst 'META-INF/**'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.facebook.react:react-native:+'
}
